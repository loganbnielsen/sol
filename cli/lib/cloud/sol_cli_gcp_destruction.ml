open Sol_cli_destroy_verification
open Sol_cli_destruction
open Sol_cli_terraform_steps
open Result.Syntax

let gcp_peering_probe ~project ~network =
  match
    Sol_cli_process.run
      (Sol_cli_process.cmd
         [ "gcloud"
         ; "services"
         ; "vpc-peerings"
         ; "list"
         ; "--network=" ^ network
         ; "--service=servicenetworking.googleapis.com"
         ; "--project"
         ; project
         ; "--format=value(peering)"
         ])
  with
  | Ok result ->
    let peerings =
      String.split_on_char '\n' result.stdout
      |> List.map String.trim
      |> List.filter (fun peering -> peering <> "" && peering <> "---")
    in
    if peerings = []
    then Probe_gone
    else
      Probe_found
        (Printf.sprintf
           "the service-networking peering survived the destroy: %s"
           (String.concat ", " peerings))
  | Error (Sol_cli_process.Non_zero _ as error)
    when Sol_cli_gcloud.classify ~project error = Not_found -> Probe_gone
  | Error (Sol_cli_process.Non_zero result) ->
    Probe_indeterminate
      (Printf.sprintf
         "the service-networking peering could not be checked: %s"
         (String.trim result.stderr))
  | Error _ ->
    Probe_indeterminate
      "the service-networking peering could not be checked: gcloud is unavailable"
;;

let relinquished_residue_probes =
  [ "google_service_networking_connection.sql", gcp_peering_probe ]
;;

let gcp_orphan_sweep ~pre_destroy ~(target_cfg : Sol_cli_config.target) =
  let project =
    List.assoc_opt "gcp" target_cfg.provider_fields
    |> Option.map (List.assoc_opt "project_id")
    |> Option.join
    |> Option.map String.trim
    |> Option.to_list
    |> List.find_opt (fun project -> project <> "")
  in
  match state_name pre_destroy "google_compute_network", project with
  | Some network, Some project ->
    orphan_sweep
      (List.map (fun (_, probe) -> probe ~project ~network) relinquished_residue_probes)
  | None, _ ->
    orphan_sweep
      ~gaps:
        [ "the GCP residue check could not name the target's VPC from Terraform state, \
           so the service-networking peering check was not run"
        ]
      []
  | Some _, None ->
    orphan_sweep
      ~gaps:
        [ "the GCP residue check could not establish the target's project (the target \
           declares no gcp.project_id), so the service-networking peering check was not \
           run"
        ]
      []
;;

let gcp_prepare_destroy_result ~guarded run_log infra_dir var_files vars state
  : unit Sol_cli_cloud_lifecycle.preparation_outcome
  =
  let open Sol_cli_cloud_lifecycle in
  let open Sol_cli_cloud_destroy in
  let failed reason = Preparation_failed { reason; policy = Continue_to_destroy } in
  let report_unrepresented unrepresented =
    match unrepresented with
    | [] -> ()
    | addresses ->
      Sol_cli_report.app
        "  WARNING: %d resource(s) this target declares are ABSENT from its state and \
         will therefore NOT be destroyed: %s\n\
        \  They may still exist in the provider and remain billable (FND-0030)."
        (List.length addresses)
        (String.concat ", " addresses)
  in
  match substrate_presence state with
  | Substrate_unknown ->
    Sol_cli_report.app "  prepare: could not read this target's state; preparing nothing.";
    failed
      "the target's Terraform state could not be read, so no deletion guard could be \
       lowered"
  | Substrate_present | Substrate_absent ->
    let represented = addresses state in
    let desired = guarded in
    report_unrepresented
      (Sol_cli_cloud_lifecycle.preparations_unrepresented ~state:represented ~desired);
    (match Sol_cli_cloud_lifecycle.preparations_eligible ~state:represented ~desired with
     | [] ->
       Sol_cli_report.app
         "  prepare: no guarded resource in this target's state, nothing is targeted.";
       Nothing_to_prepare
     | first :: rest ->
       Sol_cli_report.app
         "  prepare: disabling the deletion guards on %s..."
         (String.concat ", " (first :: rest));
       (match
          apply_asserted
            ~run_log
            ~phase_name:"gcp-destroy-prepare"
            ~policy:(guard_preparation_policy ~addresses:(first :: rest))
            ~scope:(Sol_cli_terraform.targets first rest)
            ~chdir:infra_dir
            ~var_files
            ~vars:
              (vars @ [ "sql_deletion_protection=false"; "gke_deletion_protection=false" ])
            ()
        with
        | Error message -> failed message
        | Ok () -> Prepared ()))
;;

let verify_gcp_destroy_preparation_result infra_dir =
  let* state = read_cloud_state infra_dir in
  let open Sol_cli_cloud_destroy in
  let guard address =
    Option.bind (find_address state address) (fun resource ->
      resource.deletion_protection)
  in
  if
    find_address state "google_sql_database_instance.postgres" = None
    && find_address state "google_container_cluster.main" = None
  then Error "GCP destroy preparation ran but no guarded resource is in state"
  else
    let* () =
      match guard "google_sql_database_instance.postgres" with
      | Some true ->
        Error "Cloud SQL deletion protection is still enabled after preparation"
      | Some false | None -> Ok ()
    in
    let* () =
      match guard "google_container_cluster.main" with
      | Some true -> Error "GKE deletion protection is still enabled after preparation"
      | Some false | None -> Ok ()
    in
    Sol_cli_report.app
      "  verify preparation: Cloud SQL and GKE deletion protection disabled.";
    Ok ()
;;

let observe_retention ~retention =
  match retention with
  | Sol_cli_cloud_lifecycle.Retain_nothing ->
    Retention_not_required
      "none declared, and there is no GCP snapshot surface to observe -- Cloud SQL \
       deletes its backups together with the instance (no final backup is requested), \
       and the observability buckets were created with soft delete off (retention 0, \
       INFRA-077), so the verified absence of the instance is the whole guarantee \
       (destroy_retention = none)"
  | Sol_cli_cloud_lifecycle.Retain_final_snapshot ->
    Retention_unknown
      "this destroy reached verification with destroy_retention = final-snapshot on GCP, \
       which cannot retain anything: the block was not applied, so no retention \
       guarantee can be observed"
;;

let prepare { run_log; infra_dir; var_files; vars; _ } ~retention ~cluster_name:_ ~state =
  match retention with
  | Sol_cli_cloud_lifecycle.Retain_final_snapshot ->
    Sol_cli_cloud_lifecycle.Preparation_failed
      { reason =
          "this GCP target's destroy_retention is final-snapshot (the default), but Sol \
           cannot retain anything on GCP yet: Cloud SQL deletes its backups together \
           with the instance, so there is no final-artifact equivalent of the RDS \
           snapshot and the recovery data would be discarded without saying so. Declare \
           `destroy_retention: none` on a disposable target, or export the database \
           first -- Sol will not decide this for you"
      ; policy = Sol_cli_cloud_lifecycle.Block_destroy
      }
  | Sol_cli_cloud_lifecycle.Retain_nothing ->
    (match
       gcp_prepare_destroy_result
         ~guarded:Sol_cli_provider_capabilities.gcp.guarded_addresses
         run_log
         infra_dir
         var_files
         vars
         state
     with
     | Sol_cli_cloud_lifecycle.Prepared () ->
       (match verify_gcp_destroy_preparation_result infra_dir with
        | Ok () ->
          Sol_cli_cloud_lifecycle.Prepared
            (Sol_cli_cloud_destroy.Prepared { retained = None })
        | Error reason ->
          Sol_cli_cloud_lifecycle.Preparation_failed
            { reason; policy = Sol_cli_cloud_lifecycle.Continue_to_destroy })
     | Sol_cli_cloud_lifecycle.Nothing_to_prepare ->
       Sol_cli_cloud_lifecycle.Nothing_to_prepare
     | Sol_cli_cloud_lifecycle.Preparation_failed failure ->
       Sol_cli_cloud_lifecycle.Preparation_failed failure)
;;

let destruction ctx : Sol_cli_destruction.t =
  { prepare = prepare ctx
  ; retention =
      (fun ~retention ~pre_destroy:_ ~preparation:_ -> observe_retention ~retention)
  ; residue =
      (fun ~pre_destroy ~cluster:_ ->
        gcp_orphan_sweep ~pre_destroy ~target_cfg:ctx.target)
  ; before_substrate_destroy = ignore
  }
;;
